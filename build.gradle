group 'test'
version '1.0'

apply plugin: 'java'

sourceCompatibility = 1.8

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

//Custom task here, so gradle can run cucumber
//see : https://github.com/cucumber/cucumber-jvm/tree/master/examples/java-gradle
task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output

            //default : run all
            if (!project.hasProperty("featureFile")) {
                args = ['--plugin', 'pretty', '--glue', 'config.cucumber', 'src/test/resources/features']
            } else {
                def featureFName = project.property("featureFile") as String
                def featureOptions = 'src/test/resources/features/' + featureFName
                args = ['--plugin', 'pretty', '--glue', 'config.cucumber', featureOptions]
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'io.appium:java-client:4.0.0'
    testCompile 'info.cukes:cucumber-java:1.2.4'
    testCompile 'info.cukes:cucumber-junit:1.2.4'
}
